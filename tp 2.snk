[Subsistema nuevo]
TMPTOT	TABLE		M1,3600,1800,12
GENBOL	TABLE		S$BOLETERIA,20,20,100
GENEST	TABLE		S$ESTADIO,20,20,100
TMPQENTR	QTABLE	CONTROL,0,60,30
BANDAS	MATRIX	,4,1

TIPO 		FUNCTION	RN1,D4 
.3,1/.5,2/.9,3/1,4
MEDTREC	FUNCTION	P$TIPO,D4
1,40/2,45/3,50/4,40
DESVTREC	FUNCTION	P$TIPO,D4
1,5/2,4/3,8/4,6
MEDTBIS	FUNCTION	P$TIPO,D4
1,20/2,15/3,8/4,12
DESVTBIS	FUNCTION	P$TIPO,D4
1,1/2,3/4,0/5,0
GENBUS	VARIABLE	RN3@7+4
RANDOMCAJA	VARIABLE	RN4@4+1
CONTROL 	STORAGE 	5 
ESTADIO 	STORAGE	50000
BOLETERIA 	STORAGE	50000

		GENERATE 	30,10					;A PIE
		TEST L	C1,5400,NOMAS			;A LAS 19 30 DEJAN DE LLEGAR
		ASSIGN 	MEDIO,1				;GUARDO EL MEDIO EN EL QUE LLEGARON
		TRANSFER  	,ENTRA		 
		
		GENERATE 	300,60,,25					;EN BUS
		ASSIGN 	MEDIO,2
		ASSIGN 	CANT,V$GENBUS
		ASSIGN 	CANTAUX,P$CANT			;PARA PODER HACER EL SPLIT Y NO PERDER LA CANT
OTBAJA	ADVANCE	2,1 			
		SPLIT 	1,REINICIO
		LOOP 		CANTAUX,OTBAJA
		ADVANCE 	18000,3600				;EL BUS SE VA Y LUEGO VUELVE A BUSCARLOS
		TRANSFER	,ENCUENTRO
REINICIO	MARK							;VER SI BIEN UBICADO ACA 
ENTRA		TRANSFER	.65,,ZONARECIT			;ACA SE FIJA SI TIENE BOLETO
		ADVANCE 	60,5
		ENTER 	BOLETERIA
		TRANSFER	.95,,SELECCION
		ASSIGN	1,V$RANDOMCAJA			;ELIGEN LA CAJA AL AZAR
		TRANSFER	,CAJA
SELECCION	SELECT NU	1,1,4,,,OCUP			;SE ELIGE LA CAJA PARA COMPRAR BOLETO
		TRANSFER	,CAJA
OCUP 		SELECT MIN 1,1,4,,Q
CAJA 		QUEUE		*1
		SEIZE		*1
		DEPART 	*1
		ADVANCE	120,30
		RELEASE	*1					;AGREGO ESTE RELEASE
		LEAVE 	BOLETERIA
ZONARECIT 	ADVANCE 	40,10
		QUEUE 	CONTROL
		ENTER 	CONTROL
		DEPART 	CONTROL
		ADVANCE 	90,30 				;TIEMPO DEL CONTROL
		LEAVE 	CONTROL		
		ENTER 	ESTADIO							
		ASSIGN	TIPO,FN$TIPO
		MSAVEVALUE	BANDAS+,P$TIPO,1,1		;ALMACENO CANT DE PERSONAS PARA CADA BANDA
		GATE LS	ABRESOPORTE				;COMIENZAN A TOCAR A LAS 8
		GATE LS	P$TIPO
		TEST E	P$MEDIO,2,APIE
		ASSIGN 	CANTAUX,P$CANT
		ASSIGN	CANTAUX-,1
		ASSEMBLE 	P$CANT				
		ADVANCE	300,60
		SPLIT		P$CANTAUX,PRINCIPAL
		TRANSFER	,PRINCIPAL				;PARA QUE LA TRANSACCION ORIGINAL VAYA TAMBIEN A PRINCIPAL
APIE		ADVANCE 	180,60
PRINCIPAL	GATE LS	ABREPPAL							;COMIENZA BANDA PRINCIPAL A LAS 21 30
		GATE LS	FINPPAL
		TEST E	P$MEDIO,1,SALIDABUS
		ADVANCE	600,180
		LEAVE		ESTADIO
		TRANSFER	,FIN
SALIDABUS	ADVANCE	480,120
		LEAVE		ESTADIO
		ASSEMBLE	P$CANT
ENCUENTRO	ASSEMBLE 	2					;PASAJEROS ESPERAN AL BUS O VICEVERSA
		ADVANCE 	180,60
FIN		TABULATE	TMPTOT
NOMAS		TERMINATE
;---------SUBSISTEMA BOLETERIA---------------------
		GENERATE	300
		TABULATE	GENBOL
		TERMINATE
;---------SUBSISTEMA GENTE EN ESTADIO-----------
		GENERATE	120,,,105				;105 ES LA CANTIDAD DE DOS MINUTOS DE 18 A 21 30
		TABULATE	GENEST
		TERMINATE
;---------SUBSISTEMA PARA CONTROL DE COMIENZO RECITALES---------
		GENERATE	,,7200,1				;20 HS
		LOGIC S 	ABRESOPORTE
		ADVANCE 	5400					;21 30 HS
		LOGIC S	ABREPPAL
		ADVANCE 	7200,600
		LOGIC S	FINPPAL				;FIN RECITAL PRINCIPAL 	
		TERMINATE
;---------SUBSISTEMA PARA CONTROL FIN DE RECITAL TIPO 1-------
		GENERATE	,,7200,1				;20 HS
		ADVANCE	2400,300
		ADVANCE	1200,60
		LOGIC S	1
		TERMINATE
;---------SUBSISTEMA PARA CONTROL FIN DE RECITAL TIPO 2-------
		GENERATE	,,7200,1				;20 HS
		ADVANCE	2700,240
		ADVANCE	900,180
		LOGIC S	2
		TERMINATE
;---------SUBSISTEMA PARA CONTROL FIN DE RECITAL TIPO 3-------
		GENERATE	,,7200,1				;20 HS
		ADVANCE	3000,480
		ADVANCE	480
		LOGIC S	3
		TERMINATE
;---------SUBSISTEMA PARA CONTROL FIN DE RECITAL TIPO 4-------
		GENERATE	,,7200,1				;20 HS
		ADVANCE	2400,360
		ADVANCE	720
		LOGIC S	4
		TERMINATE
;---------SUBSISTEMA PARA CONTROLAR DURACION--------
		GENERATE	,,400,1				;OFFSET DE 400 SEGS PARA Q NO TERMINE EL PROGRAMA APENAS EMPIEZA
		GATE SE	ESTADIO				;LA SIMULACION TERMINA CUANDO SE VA LA ULTIMA PERSONA
		TERMINATE	1

<\F>
Nombre=
Descripcion=
Version=
Autor=
Empresa=
Opciones=
